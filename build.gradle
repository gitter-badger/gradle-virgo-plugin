apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

group = 'com.github.jgriff.gradle'
description = "A Gradle plugin for easily assembling your own custom distributions of Eclipse Virgo."

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
    from sourceSets.integTest.output
}

task reportZip(type: Zip) {
    classifier = 'reports'
    from reporting.baseDir
}

task copyDependencies(type: Copy) {
    into "$buildDir/dependencies"
    from configurations.runtime
}

task classesAndResources(type: Copy) {
    into "$buildDir/classes-and-resources"
    from "$buildDir/classes/main"
    from "$buildDir/resources/main"
}

task integTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    dependsOn copyDependencies, classesAndResources
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

    // this is required in order to run the "GradleConnector" so it knows about our plugin
    systemProperty 'test.functional.classes', classesAndResources.destinationDir
    systemProperty 'test.functional.dependencies', copyDependencies.destinationDir
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile("com.jamesmurty.utils:java-xmlbuilder:1.1")
    testCompile("org.spockframework:spock-core:0.7-groovy-2.0@jar")
    testCompile("xmlunit:xmlunit:1.5")
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives testJar
    archives reportZip
}

// publication
bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']

    publish = true
    pkg {
        repo = 'maven'
        name = project.group + ':' + project.name
        description = project.description
        websiteUrl = 'https://github.com/jgriff/gradle-virgo-plugin'
        issueTrackerUrl = 'https://github.com/jgriff/gradle-virgo-plugin/issues'
        vcsUrl = 'https://github.com/jgriff/gradle-virgo-plugin.git'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin', 'virgo']
        version {
            name = project.version
            attributes = ['gradle-plugin': ['com.github.jgriff.virgo.assembly:com.github.jgriff.gradle:gradle-virgo-plugin']]
        }
    }
}

task wrapper(type: Wrapper) {
  description = "Builds the gradlew wrapper."
  gradleVersion = '2.0'
}
